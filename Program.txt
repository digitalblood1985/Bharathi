      *                                                                         
      ******************************************************************        
      * IDENTIFICATION   DIVISION.                                     *        
      ******************************************************************        
       ID DIVISION.                                                             
       PROGRAM-ID. MBSBNAN6.                                                    
       AUTHOR. NANDISH SHANABOG C S.                                            
       INSTALLATION. NOT TO BE INSTALLED.                                       
       DATE-WRITTEN. 2024/05/20.                                                
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      * ENVIRONMENT   DIVISION.                                        *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. MAINFRAME.                                              
       OBJECT-COMPUTER. MAINFRAME.                                              
       SPECIAL-NAMES.                                                           
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT INFILE1    ASSIGN TO INFILE1.                                 
           SELECT OUT-FILE   ASSIGN OUTFILE.                                    
           SELECT DEL-FILE   ASSIGN DELFILE.                                    
       I-O-CONTROL.                                                             
      *                                                                         
      ******************************************************************        
      * DATA   DIVISION.                                               *        
      ******************************************************************        
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  INFILE1                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS STANDARD                                               
           RECORDING MODE F.                                                    
        01 INFILE1-REC.                                                         
           05 HOLD-ACCT-NBR            PIC       X(30).                         
           05 HOLD-ACCT-NBR-SUFF       PIC       X(10).                         
           05 HOLD-SERV-CD             PIC       X(2).                          
           05 CASE-NBR                 PIC       X(20).                         
           05 TPA-CD                   PIC       X(10).                         
           05 HOLD-APPL-SYS-CD         PIC       X(10).                         
           05 HOLD-APPL-SUB-SYS-CD     PIC       X(10).                         
      * BUS-PROC-DT STARTS AT COLUMN 93                                         
           05 BUS-PROC-DT              PIC       X(10).                         
           05 HOLDDT-DATE-TYPE         PIC       X(4).                          
              88 EFFECTIVE-DATE                  VALUE 'EFFD'.                  
              88 INITIAL-DEPOSIT-DATE            VALUE 'INIT'.                  
              88 ISSUE-DATE                      VALUE 'ISSU'.                  
              88 LAST-FINANCIAL-DATE             VALUE 'LSTF'.                  
              88 STATUS-CHANGE-DATE              VALUE 'STCH'.                  
              88 TERMINATION-DATE                VALUE 'TERM'.                  
              88 BILLING-DATE                    VALUE 'BILL'.                  
              88 DEATH-BENEFIT-EFFECTIVE-DATE    VALUE 'DBEF'.                  
              88 LAST-DIVIDEND-DATE              VALUE 'LSTD'.                  
              88 MEC-DATE                        VALUE 'MECD'.                  
              88 MEC-THRESHOLD-START-DATE        VALUE 'MECT'.                  
              88 MATURITY-DATE                   VALUE 'MATR'.                  
              88 PAID-TO-DATE                    VALUE 'PAID'.                  
              88 LEVEL-PREMIUM-END-DATE          VALUE 'LVPE'.                  
              88 NEXT-PAYMENT-DUE-DATE           VALUE 'NPDD'.                  
              88 APPLICATION-SIGNED-DATE         VALUE 'APSD'.                  
              88 LAST-NON-FIN-PROCESS            VALUE 'LNFT'.                  
              88 LAST-PREM-EFF-DATE              VALUE 'LPED'.                  
              88 NEXT-RISK-PURCHASE-DATE         VALUE 'NRPD'.                  
              88 ANNIVERSARY-DATE                VALUE 'VAOD'.                  
              88 LOAN-INT-CAP-DATE               VALUE 'LICD'.                  
              88 MAX-REINSTATEMENT-DATE          VALUE 'MRID'.                  
              88 RA-EFF-REINSTATEMENT-DT         VALUE 'RERD'.                  
              88 FISCAL-ANNIVERSARY-DATE         VALUE 'PAND'.                  
              88 GROUP-PLAN-EFF-DATE             VALUE 'PEFD'.                  
              88 PREV-REBALANCE-DATE             VALUE 'LRBD'.                  
              88 EXPIRY-DATE                     VALUE 'EXPD'.                  
              88 NEXT-DRAFT-DATE                 VALUE 'NXDD'.                  
      *  HOLDDT-VALUE STARTS AT COLUMN 107                                      
           05 HOLDDT-VALUE             PIC       X(10).                         
           05 HOLD-COMP-CD             PIC       X(3).                          
           05 HOLD-STAT-CO-CD          PIC       X(3).                          
      *                                                                         
                                                                                
       FD  OUT-FILE                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS STANDARD                                               
           RECORDING MODE F.                                                    
       01  OUT-REC                     PIC X(122).                              
                                                                                
                                                                                
       FD  DEL-FILE                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS STANDARD                                               
           RECORDING MODE F.                                                    
       01  DEL-REC                     PIC X(122).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01 WS-HOLD-VALUE-DT.                                                     
          05 WS-HV-DT-YYYY             PIC 9(04).                               
          05 WS-DATE-SEPERATOR1        PIC X(01).                               
          05 WS-HV-DT-MM               PIC 9(02).                               
          05 WS-DATE-SEPERATOR2        PIC X(01).                               
          05 WS-HV-DT-DD               PIC 9(02).                               
                                                                                
       01 WS-DATE-VALIDITY             PIC X(01).                               
           88 VALID-DATE               VALUE 'V'.                               
           88 INVALID-DATE             VALUE 'I'.                               
                                                                                
      *                                                                         
       01 WS-TABLE.                                                             
       02 WS-TABLE1 OCCURS 1 TO 20 TIMES                                        
                          DEPENDING ON WS-ACC-CNT                               
                          INDEXED BY TBL1-IDX.                                  
           05 HOLD-ACCT-NBR            PIC       X(30).                         
           05 HOLD-ACCT-NBR-SUFF       PIC       X(10).                         
           05 HOLD-SERV-CD             PIC       X(2).                          
           05 CASE-NBR                 PIC       X(20).                         
           05 TPA-CD                   PIC       X(10).                         
           05 HOLD-APPL-SYS-CD         PIC       X(10).                         
           05 HOLD-APPL-SUB-SYS-CD     PIC       X(10).                         
           05 BUS-PROC-DT              PIC       X(10).                         
           05 HOLDDT-DATE-TYPE         PIC       X(4).                          
              88 EFFECTIVE-DATE                  VALUE 'EFFD'.                  
              88 INITIAL-DEPOSIT-DATE            VALUE 'INIT'.                  
              88 ISSUE-DATE                      VALUE 'ISSU'.                  
              88 LAST-FINANCIAL-DATE             VALUE 'LSTF'.                  
              88 STATUS-CHANGE-DATE              VALUE 'STCH'.                  
              88 TERMINATION-DATE                VALUE 'TERM'.                  
              88 BILLING-DATE                    VALUE 'BILL'.                  
              88 DEATH-BENEFIT-EFFECTIVE-DATE    VALUE 'DBEF'.                  
              88 LAST-DIVIDEND-DATE              VALUE 'LSTD'.                  
              88 MEC-DATE                        VALUE 'MECD'.                  
              88 MEC-THRESHOLD-START-DATE        VALUE 'MECT'.                  
              88 MATURITY-DATE                   VALUE 'MATR'.                  
              88 PAID-TO-DATE                    VALUE 'PAID'.                  
              88 LEVEL-PREMIUM-END-DATE          VALUE 'LVPE'.                  
              88 NEXT-PAYMENT-DUE-DATE           VALUE 'NPDD'.                  
              88 APPLICATION-SIGNED-DATE         VALUE 'APSD'.                  
              88 LAST-NON-FIN-PROCESS            VALUE 'LNFT'.                  
              88 LAST-PREM-EFF-DATE              VALUE 'LPED'.                  
              88 NEXT-RISK-PURCHASE-DATE         VALUE 'NRPD'.                  
              88 ANNIVERSARY-DATE                VALUE 'VAOD'.                  
              88 LOAN-INT-CAP-DATE               VALUE 'LICD'.                  
              88 MAX-REINSTATEMENT-DATE          VALUE 'MRID'.                  
              88 RA-EFF-REINSTATEMENT-DT         VALUE 'RERD'.                  
              88 FISCAL-ANNIVERSARY-DATE         VALUE 'PAND'.                  
              88 GROUP-PLAN-EFF-DATE             VALUE 'PEFD'.                  
              88 PREV-REBALANCE-DATE             VALUE 'LRBD'.                  
              88 EXPIRY-DATE                     VALUE 'EXPD'.                  
              88 NEXT-DRAFT-DATE                 VALUE 'NXDD'.                  
           05 HOLDDT-VALUE             PIC       X(10).                         
           05 HOLD-COMP-CD             PIC       X(3).                          
           05 HOLD-STAT-CO-CD          PIC       X(3).                          
      *                                                                         
                                                                                
       01 WS-ACC-CNT                            PIC S9(09) COMP.                
       01 WS-LAST-READ-ACCOUNT                  PIC X(30).                      
                                                                                
       01 EOF-INFILE1                           PIC 9(01).                      
           88 EOF-INFILE1-Y                     VALUE 1.                        
       01 NEXT-ACCOUNT                          PIC 9(01).                      
           88 NEXT-ACCOUNT-Y                    VALUE 1.                        
                                                                                
       01 WS-IDX1                               PIC S9(04) COMP.                
       01 WS-IDX2                               PIC S9(04) COMP.                
                                                                                
       LOCAL-STORAGE SECTION.                                                   
       LINKAGE SECTION.                                                         
      *                                                                         
      ******************************************************************        
      * PROCEDURE   DIVISION.                                          *        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
       MAIN-PARA.                                                               
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-PROCESS.                                                
           PERFORM 3000-FINALIZE.                                               
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZATION.                                                     
           SAPIENS-OPEN.                                                        
           OPEN INPUT  INFILE1.                                                 
           OPEN OUTPUT OUT-FILE.                                                
           OPEN OUTPUT DEL-FILE.                                                
                                                                                
           MOVE 0 TO EOF-INFILE1                                                
                     NEXT-ACCOUNT                                               
                                                                                
           MOVE SPACES TO WS-DATE-VALIDITY                                      
           .                                                                    
                                                                                
       2000-PROCESS.                                                            
      * FIRST READ                                                              
           READ INFILE1                                                         
             AT END SET EOF-INFILE1-Y TO TRUE                                   
             NOT AT END                                                         
               MOVE 1 TO WS-ACC-CNT                                             
               PERFORM POPULATE-TABLE                                           
           END-READ                                                             
                                                                                
      * SUBSEQUENT READS - IN LOOP                                              
           PERFORM UNTIL EOF-INFILE1-Y                                          
              READ INFILE1                                                      
                 AT END SET EOF-INFILE1-Y TO TRUE                               
                 NOT AT END                                                     
                    PERFORM CHECK-FOR-NEXT-ACCOUNT                              
              END-READ                                                          
                                                                                
              IF NOT EOF-INFILE1-Y                                              
                  IF NOT NEXT-ACCOUNT-Y                                         
                     ADD  1 TO WS-ACC-CNT                                       
                  ELSE                                                          
                     PERFORM PROCESS-RECORDS-FOR-AN-ACCOUNT                     
                     MOVE 1 TO WS-ACC-CNT                                       
                     MOVE 0 TO NEXT-ACCOUNT                                     
                  END-IF                                                        
                  PERFORM POPULATE-TABLE                                        
              END-IF                                                            
           END-PERFORM                                                          
                                                                                
           PERFORM PROCESS-RECORDS-FOR-AN-ACCOUNT                               
           .                                                                    
                                                                                
                                                                                
       POPULATE-TABLE.                                                          
           MOVE HOLD-ACCT-NBR OF INFILE1-REC TO                                 
                WS-LAST-READ-ACCOUNT                                            
           MOVE INFILE1-REC TO WS-TABLE1                                        
                (WS-ACC-CNT)                                                    
           .                                                                    
                                                                                
                                                                                
       CHECK-FOR-NEXT-ACCOUNT.                                                  
           IF HOLD-ACCT-NBR OF INFILE1-REC NOT =                                
               WS-LAST-READ-ACCOUNT                                             
               SET NEXT-ACCOUNT-Y TO TRUE                                       
           END-IF                                                               
           .                                                                    
                                                                                
                                                                                
       PROCESS-RECORDS-FOR-AN-ACCOUNT.                                          
           SET VALID-DATE TO TRUE                                               
           DISPLAY 'WS-ACC-CNT: ' WS-ACC-CNT                                    
           PERFORM VARYING WS-IDX1 FROM 1 BY 1 UNTIL                            
                           WS-IDX1 > WS-ACC-CNT                                 
               DISPLAY WS-TABLE1(WS-IDX1)(1:30)                                 
                       WS-TABLE1(WS-IDX1)(107:10)                               
               EVALUATE WS-TABLE1(WS-IDX1)(107:10)                              
                  WHEN "9999-12-31"                                             
                  WHEN "1900-01-01"                                             
                     CONTINUE                                                   
                  WHEN OTHER                                                    
                     PERFORM 2100-DATE-VALIDATION                               
               END-EVALUATE                                                     
           END-PERFORM                                                          
                                                                                
           IF VALID-DATE                                                        
              PERFORM VARYING WS-IDX2 FROM 1 BY 1 UNTIL                         
                              WS-IDX2 > WS-ACC-CNT                              
                  WRITE OUT-REC FROM WS-TABLE1(WS-IDX2)                         
              END-PERFORM                                                       
           ELSE                                                                 
              PERFORM VARYING WS-IDX2 FROM 1 BY 1 UNTIL                         
                              WS-IDX2 > WS-ACC-CNT                              
                  WRITE DEL-REC FROM WS-TABLE1(WS-IDX2)                         
              END-PERFORM                                                       
           END-IF                                                               
           .                                                                    
                                                                                
                                                                                
       2100-DATE-VALIDATION.                                                    
           MOVE WS-TABLE1(WS-IDX1)(107:10) TO WS-HOLD-VALUE-DT                  
                                                                                
      * THE YEAR, MONTH AND DAY SHOULD BE NUMERIC                               
           IF WS-HV-DT-YYYY             IS NOT NUMERIC OR                       
              WS-HV-DT-MM               IS NOT NUMERIC OR                       
              WS-HV-DT-DD               IS NOT NUMERIC                          
               SET INVALID-DATE TO TRUE                                         
           END-IF                                                               
                                                                                
      * THE DATE SEPERATORS SHOULD BE '-'                                       
           IF WS-DATE-SEPERATOR1        NOT = "-" OR                            
              WS-DATE-SEPERATOR2        NOT = "-"                               
               SET INVALID-DATE TO TRUE                                         
           END-IF                                                               
                                                                                
      * YEAR VALIDATION (IT CANT BE MORE THAN 9999 - AS THERE ARE JUST          
      *                  4 DIGITS. HENCE CHECKING FOR < 1900 ALONE)             
           IF WS-HV-DT-YYYY             < "1900"                                
               SET INVALID-DATE TO TRUE                                         
           END-IF                                                               
                                                                                
      * MONTH VALIDATION                                                        
           IF NOT (WS-HV-DT-MM               >= "01" AND                        
                   WS-HV-DT-MM               <= "12")                           
               SET INVALID-DATE TO TRUE                                         
           END-IF                                                               
                                                                                
      * DAY VALIDATION - GENERAL                                                
           IF WS-HV-DT-DD               < "01" OR > "31"                        
               SET INVALID-DATE TO TRUE                                         
           END-IF                                                               
                                                                                
      * DAY VALIDATION - MONTH WISE                                             
           EVALUATE WS-HV-DT-MM                                                 
               WHEN "04"                                                        
               WHEN "06"                                                        
               WHEN "09"                                                        
               WHEN "11"                                                        
                   IF WS-HV-DT-DD               < "01" OR > "30"                
                       SET INVALID-DATE TO TRUE                                 
                   END-IF                                                       
               WHEN "02"                                                        
                   IF FUNCTION MOD(WS-HV-DT-YYYY, 4) = 0                        
                       IF WS-HV-DT-DD               < "01" OR > "29"            
                           SET INVALID-DATE TO TRUE                             
                       END-IF                                                   
                   ELSE                                                         
                       IF WS-HV-DT-DD               < "01" OR > "28"            
                           SET INVALID-DATE TO TRUE                             
                       END-IF                                                   
                   END-IF                                                       
               WHEN "01"                                                        
               WHEN "03"                                                        
               WHEN "05"                                                        
               WHEN "07"                                                        
               WHEN "08"                                                        
               WHEN "10"                                                        
                   IF WS-HV-DT-DD               < "01" OR > "31"                
                       SET INVALID-DATE TO TRUE                                 
                   END-IF                                                       
               WHEN OTHER                                                       
                   SET INVALID-DATE TO TRUE                                     
           END-EVALUATE                                                         
                                                                                
           MOVE SPACES TO WS-HOLD-VALUE-DT                                      
           .                                                                    
                                                                                
       3000-FINALIZE.                                                           
           SAPIENS-CLOSE.                                                       
           CLOSE INFILE1.                                                       
           CLOSE OUT-FILE.                                                      
           CLOSE DEL-FILE.                                                      
           .                                                                    
